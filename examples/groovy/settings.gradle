buildscript {
  repositories { flatDir { dirs 'libs' } }
  dependencies { classpath fileTree(include:['*.jar'], dir: 'libs') }
}

apply plugin: 'dev.aga.gradle.version-catalog-generator'

dependencyResolutionManagement {
  repositories {
    mavenCentral()
  }
  versionCatalogs {
      create("myLibs") { from(files("gradle/catalog.versions.toml")) }
      // test appending to existing catalog
      generator.generate("myLibs") {
        it.fromToml("aws-bom")
        it.using { u ->
          u.aliasPrefixGenerator = NO_PREFIX
        }
        it.bundle { lib ->
          if(lib.alias in ["sts", "commons-csv"]) {
            "merged"
          } else {
            lib.versionRef
          }
        }
      }
      generator.generate("jsonLibs") {
        it.from("com.fasterxml.jackson:jackson-bom:2.15.2")
        it.using { u ->
          u.libraryAliasGenerator = { groupId, artifactId ->
            def prefix = u.aliasPrefixGenerator.invoke(groupId, artifactId)
            def suffix = u.aliasSuffixGenerator.invoke(prefix, groupId, artifactId)
            DEFAULT_ALIAS_GENERATOR.invoke(prefix,suffix)
          }
          u.versionNameGenerator = DEFAULT_VERSION_NAME_GENERATOR
       }
       bundle("jacksonModule") { lib -> lib.name.contains("module") }
    }

    generator.generate("mockitoLibs") {
      it.from("org.mockito:mockito-bom:5.5.0")
      it.using { u ->
        u.generateBomEntry = true
        u.libraryAliasGenerator = { groupId, artifactId ->
          def prefix = u.aliasPrefixGenerator.invoke(groupId, artifactId)
          def suffix = u.aliasSuffixGenerator.invoke(prefix, groupId, artifactId)
          DEFAULT_ALIAS_GENERATOR.invoke(prefix,suffix)
        }
        u.versionNameGenerator = DEFAULT_VERSION_NAME_GENERATOR
      }
    }
    generator.generate("awsLibs") {
      it.fromToml("aws-bom")
      it.using { u->
          u.aliasPrefixGenerator = NO_PREFIX
      }
    }
    generator.generate("springLibs") { gen ->
      gen.fromToml("spring-boot-dependencies")
      gen.using { u ->
        u.propertyOverrides = [
                "jackson-bom.version": u.versionRef("jackson")
        ]
      }
    }
    generator.generate("junitLibs") {
      it.from { from ->
        from.toml { toml ->
          toml.libraryAliases = ["boms-junit5"]
          toml.file = toml.artifact("io.micronaut.platform:micronaut-platform:4.3.6")
        }
      }
      it.using { u ->
        u.aliasPrefixGenerator = NO_PREFIX
      }
    }
    generator.generate("manyBoms") {
      it.from { from ->
        from.toml { toml ->
          toml.libraryAliases = ["spring-boot-dependencies"]
        }
        from.using { u ->
          u.propertyOverrides = [
                  "jackson-bom.version": u.versionRef("jackson")
          ]
        }
      }

      it.from { from ->
        from.toml { toml ->
          toml.libraryAliases = ["aws-bom", "jackson-bom"]
        }
        from.using { u ->
          u.aliasPrefixGenerator = NO_PREFIX
        }
      }
    }
  }
}
